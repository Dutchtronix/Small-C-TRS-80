PAGE	66
	TITLE	SMALL-C	RUN-TIME	LIBRARY
TRUE	EQU	1
	SUBTTL	VARIABLE	MOVING	FUNCTIONS
	PAGE
CCC1::	LD	A,(HL)
CCC5::	LD	L,A
	RLA
	SBC	A,A
	LD	H,A
	OR	L
	RET
;
; CCGIB Get integer with SP offset in L
;
CCC50::
	ld	h,0		;make word
	inc	hl		;correct for return address
	inc	hl
	add	hl,sp
;
; fall through
;
CCGINT:
CCC2::	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	OR	H
	RET
CCC39::	INC	HL
	INC	HL
	ADD	HL,SP
	LD	D,H
	LD	E,L
	CALL	CCGINT
	DEC	HL
	JR	CCPINT
CCC44::	INC	HL
	INC	HL
	ADD	HL,SP
	LD	D,H
	LD	E,L
	CALL	CCGINT
	INC	HL
	JR	CCPINT
CCPINT:
CCC3::	LD	A,L
	LD	(DE),A
	INC	DE
	LD	A,H
	LD	(DE),A
	OR	L
	RET
CCC32::	JP	(HL)
	PAGE
	SUBTTL	ARITHMETIC	FUNCTIONS
CCC29::
	LD	A,E
	SUB	L
	LD	L,A
	LD	A,D
	SBC	A,H
	LD	H,A
	RET
CCC22::	LD	A,H
	LD	C,L
	LD	HL,0
	OR	A
	CALL	NZ,MUL8
	LD	A,C
	CALL	MUL8
	RET
MUL8:	LD	B,08H
MUL81:	ADD	HL,HL
	RLCA
	JR	NC,MUL82
	ADD	HL,DE
MUL82:	DJNZ	MUL81
	JP	COMOUT
CCC14::	EX	DE,HL
	LD	B,D
	LD	C,E
DIV16:	LD	A,B
	OR	C
	JR	Z,DVCK
	XOR	A
	ADD	A,B
	PUSH	AF
	CALL	P,NEGB
	XOR	A
	ADD	A,H
	PUSH	AF
	CALL	M,CCNEG
	EX	DE,HL
	LD	HL,0000H
	LD	A,10H
D2:	ADD	HL,HL
	EX	DE,HL
	ADD	HL,HL
	EX	DE,HL
	JR	NC,D3
	INC	HL
D3:	PUSH	HL
	ADD	HL,BC
	JR	NC,D4
	INC	E
	INC	SP
	INC	SP
	DEC	A
	JR	NZ,D2
	JR	D4A
D4:	POP	HL
	DEC	A
	JR	NZ,D2
D4A:	EX	DE,HL
	POP	BC
	POP	AF
	XOR	B
	CALL	M,CCNEG
	LD	A,D
	OR	E
	JR	Z,COMOUT
	XOR	A
	ADD	A,B
	JP	P,COMOUT
	XOR	A
	SUB	E
	LD	E,A
	SBC	A,D
	SUB	E
	LD	D,A
	JR	COMOUT
DVCK:	LD	HL,MESS1
DV05:	LD	A,(HL)
	OR	A
	JR	Z,DV10
	CALL	33H
	INC	HL
	JR	DV05
DV10:	LD	HL,0
	LD	D,H
	LD	E,L
	JR	COMOUT
MESS1:	DB	'division by zero'
	DB	13
NEGB:	LD	A,B
	CPL
	LD	B,A
	LD	A,C
	CPL
	LD	C,A
	INC	BC
	RET
CCC23::	LD	A,H
	OR	D
	LD	H,A
	LD	A,L
	OR	E
	LD	L,A
	OR	H
	RET
CCC10::	LD	A,H
	AND	D
	LD	H,A
	LD	A,L
	AND	E
	LD	L,A
	OR	H
	RET
CCC30::	LD	A,L
	OR	H
	JR	NZ,CCL10
	LD	HL,1
	OR	L
	RET
CCL10:	LD	HL,0
	XOR	A
	RET
CCC28::	LD	A,H
	XOR	D
	LD	H,A
	LD	A,L
	XOR	E
	LD	L,A
	OR	H
	RET
CCNEG:
CCC21::	CALL	CCCOM
	INC	HL
	JR	COMOUT
CCCOM:
CCC13::	LD	A,H
	CPL
	LD	H,A
	LD	A,L
	CPL
	LD	L,A
	OR	H
	RET
COMOUT:	LD	A,H
	OR	L
	RET
CCC11::	EX	DE,HL
	XOR	A
	ADD	A,E
	JR	Z,COMOUT
	JP	M,SR1
SL1:	ADD	HL,HL
	DEC	A
	JR	NZ,SL1
	JR	COMOUT
CCC12::	EX	DE,HL
	XOR	A
	SUB	E
	JR	Z,COMOUT
	JP	P,SL1
	LD	C,A
SR1:	SRA	H
	RR	L
	INC	C
	JR	NZ,SR1
	JR	COMOUT
CCC31::	EX	DE,HL
	XOR	A
	SUB	E
	JR	Z,COMOUT
	JP	P,SL1
	LD	C,A
USR1:	SRL	H
	RR	L
	INC	C
	JR	NZ,USR1
	JR	COMOUT
CCUCMP:	LD	A,D
	CP	H
	JR	NZ,CCUCP2
	LD	A,E
	CP	L
CCUCP2:	LD	HL,TRUE
	RET
CCULT1:
CCC24::	CALL	CCUCMP
	JR	C,COMOUT
	DEC	HL
	XOR	A
	RET
CCC25::	CALL	CCUCMP
	JR	Z,COMOUT
	JR	C,COMOUT
	DEC	HL
	XOR	A
	RET
CCC26::	EX	DE,HL
	JR	CCULT1
CCC27::	CALL	CCUCMP
	JR	NC,COMOUT
	DEC	HL
	XOR	A
	RET
CCC15::	LD	A,1
	JR	EQUAL
CCC20::	XOR	A
EQUAL:	EX	DE,HL
	PUSH	AF
	XOR	A
	LD	A,L
	CP	E
	JR	NZ,AFALSE
	LD	A,H
	CP	D
	JR	NZ,AFALSE
ATRUE:	POP	AF
	LD	L,A
	LD	H,0
	OR	A
	RET
CCC16::	LD	B,00H
	LD	C,B
LESS:	EX	DE,HL
	PUSH	BC
	XOR	A
	LD	A,H
	CP	D
	JR	Z,EQH
	RRA
	XOR	H
	XOR	D
	JP	M,ATRUE
AFALSE:	POP	AF
	XOR	01H
	LD	L,A
	LD	H,0
	OR	A
	RET
EQH:	LD	A,L
	CP	E
	JR	C,ATRUE
	POP	BC
	LD	A,B
	JR	NZ,NE0
	XOR	C
NE0:	XOR	01H
	LD	L,A
	LD	H,0
	RET
CCC19::	LD	B,01H
	LD	C,00H
	JR	LESS
CCC17::	LD	B,00H
	LD	C,01H
	JR	LESS
CCC18::	LD	B,01H
	LD	C,B
	JR	LESS
	PAGE
	SUBTTL	SWITCH	STATEMENT	EXECUTION
CCC4::	EX	DE,HL
	POP	HL
SWLOOP:	LD	C,(HL)
	INC	HL
	LD	B,(HL)
	INC	HL
	LD	A,B
	OR	C
	JR	Z,SWEND
	LD	A,(HL)
	INC	HL
	CP	E
	LD	A,(HL)
	INC	HL
	JR	NZ,SWLOOP
	CP	D
	JR	NZ,SWLOOP
	LD	H,B
	LD	L,C
SWEND:	JP	(HL)
MODTST	EQU	125H
MODIII	EQU	73
EXTRN	GETCMD,	EXIT
EXTRN	MAIN
CCMAIN::
	LD	A,(MODTST)
	CP	MODIII
	JR	Z,CCM3
	LD	HL,(4049H)
	JR	CCM
CCM3:	LD	HL,(4411H)
CCM:	LD	SP,HL
IFDEF	EDTASM
	LD	HL,CCCEND
	LD	(CURHP),HL
ENDIF
	CALL	GETCMD
	CALL	MAIN
	LD	HL,0
	PUSH	HL
	CALL	EXIT
IFDEF	EDTASM
CCEND:	NOP
ENDIF
	END	CCMAIN

