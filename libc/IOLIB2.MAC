	PAGE	66
	TITLE	SMALL-C	I/O	LIBRARY
	SUBTTL	VARIOUS	I/O	FUNCTIONS
EXTRN	CCC2,	CCC5,	FWRITE,	FREAD,	GETFCB

KBFCB	EQU	4015H
DOSCAL	EQU	4419H
DOSRES	EQU	443FH
EOFILE	EQU	-1
FALSE	EQU	0
TRUE	EQU	1
CR	EQU	13
SLFCB	EQU	0
SLMODE	EQU	2
SLRSLT	EQU	3
SLFLAG	EQU	4
SLPUSH	EQU	5
SLSIZE	EQU	6
FEOF::	POP	IY
	POP	HL
	PUSH	HL
	PUSH	IY
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	LD	HL,51FFH
	RST	18H
	JR	C,FGOON
	LD	HL,KBFCB
	RST	18H
	JR	NZ,EOFON
EOFOUT:	LD	HL,FALSE
	RET
NHOP	EQU	11
NMOP	EQU	10
NLOP	EQU	5
EHOP	EQU	13
EMOP	EQU	12
ELOP	EQU	8
FGOON:	PUSH	DE
	POP	IX
	LD	A,(IX+EHOP)
	CP	(IX+NHOP)
	JR	C,EOFON
	JR	NZ,EOFOUT
	LD	A,(IX+EMOP)
	CP	(IX+NMOP)
	JR	C,EOFON
	JR	NZ,EOFOUT
	LD	A,(IX+ELOP)
	CP	(IX+NLOP)
	JR	Z,EOFON
	JR	NC,EOFOUT
EOFON:	LD	HL,TRUE
	RET
FTELL::	POP	IY
	POP	IX
	PUSH	IX
	CALL	GETFCB
	PUSH	DE
	POP	IX
	LD	H,(IX+NMOP)
	LD	L,(IX+NLOP)
	JP	(IY)
FERROR::	POP	IY
	POP	IX
	PUSH	IX
	LD	A,(IX+SLRSLT)
	CALL	CCC5
	JP	(IY)
REWIND::	POP	IY
	POP	IX
	PUSH	IX
	PUSH	IY
	CALL	GETFCB
	LD	HL,51FFH
	RST	18H
	JR	NC,RWNDOK
	CALL	DOSRES
	JR	Z,RWNDOK
	LD	(IX+SLRSLT),A
RWNDOK:	RET
SYSTEM::	LD	HL,2
	ADD	HL,SP
	CALL	CCC2
	PUSH	HL
SYLOOP:	LD	A,(HL)
	OR	A
	JR	Z,SYSOUT
	INC	HL
	JR	SYLOOP
SYSOUT:	LD	A,CR
	LD	(HL),A
	EX	(SP),HL
	CALL	DOSCAL
	POP	HL
	XOR	A
	LD	(HL),A
	RET
GETW::	POP	BC
	POP	DE
	PUSH	DE
	PUSH	BC
	PUSH	DE
	LD	BC,1
	PUSH	BC
	INC	BC
	PUSH	BC
	LD	HL,WBUF
	PUSH	HL
	CALL	FREAD
	LD	HL,6
	ADD	HL,SP
	LD	SP,HL
	POP	IX
	LD	A,(IX+SLRSLT)
	OR	A
	JR	NZ,GETW1
	LD	HL,(WBUF)
	RET
GETW1:	LD	HL,EOFILE
	RET
WBUF:	DEFW	0
PUTW::	POP	BC
	POP	HL
	POP	DE
	PUSH	DE
	PUSH	HL
	PUSH	BC
	PUSH	DE
	LD	BC,1
	PUSH	BC
	INC	BC
	PUSH	BC
	LD	(WBUF),HL
	LD	HL,WBUF
	PUSH	HL
	CALL	FWRITE
	LD	HL,8
	ADD	HL,SP
	LD	SP,HL
	RET
	END
